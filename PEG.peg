
# Hierarchical syntax
Grammar <- Spacing Definition+ Extensions
Definition <- Identifier LEFTARROW Expression
Expression <- Sequence (SLASH Sequence)*
Sequence <- Prefix*
Prefix <- (AND / NOT)? Primary (QUESTION / STAR / PLUS)? 
Primary <- Identifier !LEFTARROW
         / ActionExprExt
         / OPEN Expression CLOSE
         / Literal / Class / DOT

# Extension Syntax
Extensions <- EOF / (Extension Spacing)* EOF 
Extension <- PrefixExt / ValueExt / AuxilExt / HeaderExt / CCodeExt
PrefixExt <- '%prefix' Spacing Literal
ValueExt <- '%value' Spacing Literal
AuxilExt <- '%auxil' Spacing Literal
HeaderExt <- '%header' Spacing ExtBody Spacing
CCodeExt <- '%%' .* EOF
ExtBody <- '{' Spacing ExtBody '}' Spacing
         / '{' Spacing (!'}' .)+ '}' Spacing
         / '{' Spacing '}' Spacing
#CComment <- CSLComment / CMLComment
#CSLComment <- '//' (!EOL .)* EOL
#CMLComment <- '/*' (!'*/' .)* '*/'


ActionExprExt <- '{' Spacing ActionExprExt '}' Spacing /
                 '{' Spacing (!'}' .)* '}' Spacing

# Lexical syntax
EOF <- !.
EOL <- '\r\n' / '\n' / '\r'
Space <- ' ' / '\t' / EOL
PEGComment <- '#' (!EOL .)* EOL
Spacing <- (Space / PEGComment)*
DOT <- '.' Spacing
CLOSE <- ')' Spacing
OPEN <- '(' Spacing
PLUS <- '+' Spacing
STAR <- '*' Spacing
QUESTION <- '?' Spacing
NOT <- '!' Spacing
AND <- '&' Spacing
SLASH <- '/' Spacing
LEFTARROW <- '<-' Spacing
Char <- '\\' [nrt'"\[\]\\]
      / '\\' [0-2][0-7][0-7]
      / '\\' [0-7][0-7]?
      / !'\\' .
Range <- Char '-' Char / Char
Class <- '[' '^'? (!']' Range)* ']' Spacing
Literal <- ['] (!['] Char)* ['] Spacing
         / ["] (!["] Char)* ["] Spacing
IdentStart <- [a-zA-Z_]
IdentCont <- IdentStart / [0-9]
Identifier <- IdentStart IdentCont* Spacing


%prefix "peg"
%value "ASTNode_t*"
%auxil "auxil_t*"

%header {
#include <daisho/Daisho.h>

#define PCC_GETCHAR(auxil) nextChar(auxil)
#define PCC_BUFFERSIZE (__DAI_PAGESIZE * 10)
#define PCC_DEBUG(auxil, event, rule, level, pos, buffer, length) debugVisit(rule, level)

struct ASTNode_t;
typedef struct ASTNode_t ASTNode_t;
struct ASTNode_t {
  ASTNode_t* parent;
};

typedef struct {
  __Dai_String_View target;
} auxil_t;

static inline char nextChar(auxil_t* auxil) {
  if (auxil->target.str[auxil->target.len] == '\0') {
    return EOF;
  } else {
    char ret = auxil->target.str[auxil->target.len];
    auxil->target.len++;
    return ret;
  }
}
static inline void debugVisit(char* rule, size_t level) {
  for (size_t i = 0; i < level; i++)
    putchar(' ');
  puts(rule);
}
}


%%
int main(int argc, char** argv) {

  // Load file
  char* filename = argc == 2 ? argv[1] : "PEG.peg";
  auxil_t auxil;
  auxil.target = __Dai_readFile(filename);
  auxil.target.len = 0;
  printf("Loaded %s\n", filename);

  // Parse ast
  ASTNode_t* ast;
  peg_context_t* ctx = peg_create(&auxil);
  int parser_ret = peg_parse(ctx, &ast);
  printf("%i\n", parser_ret);
  peg_destroy(ctx);
  return 0;
}
